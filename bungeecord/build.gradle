plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'com.xinecraft'
version = '5.0.0-bungee'
description = 'Minetrax - BungeeCord'
def targetJavaVersion = 11

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.md-5.net/content/groups/public/') }
    maven { url = uri('https://oss.sonatype.org/content/groups/public/') }
}

dependencies {
    implementation 'org.bstats:bstats-bungee:2.0.0'
    implementation 'commons-codec:commons-codec:1.15'
    compileOnly 'io.netty:netty-all:4.1.86.Final'
    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
}


java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate('org.bstats', 'com.xinecraft.minetrax.bstats')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
