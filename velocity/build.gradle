dependencies {
    implementation project(":common")
    implementation 'org.bstats:bstats-velocity:3.0.2'
    compileOnly "com.velocitypowered:velocity-api:3.0.0-SNAPSHOT"
    annotationProcessor "com.velocitypowered:velocity-api:3.0.0-SNAPSHOT"
    implementation "dev.dejvokep:boosted-yaml:1.3"
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

shadowJar {
    // Additional velocity-specific relocations
    relocate('dev.dejvokep.boostedyaml', 'com.xinecraft.minetrax.libs.dejvokep.boostedyaml')
}